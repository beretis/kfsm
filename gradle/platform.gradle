/*
    Copyright 2019-2024 Open JumpCO

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated
    documentation files (the "Software"), to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software,
    and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO
    THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
 */
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

apply plugin: 'org.jetbrains.kotlin.multiplatform'
def targetList = ['mingw', 'linux', 'macos', 'js', 'jvm', 'wasm-js']

project.ext.useTarget = [:]
def useTarget = project.ext.useTarget

def arch = DefaultNativePlatform.currentArchitecture
def os = DefaultNativePlatform.currentOperatingSystem

def profile = ext.find('profile') ?: buildProfile ?: ''
def buildTargetList = profile.tokenize(',')
if (buildTargetList.contains('default')) {
    if (os.isWindows()) {
        useTarget['mingw'] = true
        logger.lifecycle "Detected ${os} using mingw"
    }
    if (os.isLinux()) {
        useTarget['linux'] = true
        logger.lifecycle "Detected ${org.gradle.internal.os.OperatingSystem.current()} using linux"
    }
    if (os.isMacOsX()) {
        useTarget['macos'] = true
        logger.lifecycle "Detected ${org.gradle.internal.os.OperatingSystem.current()} using macos"
    }
}

targetList.forEach { target ->
    if(!useTarget[target]) {
        useTarget[target] = buildTargetList.contains('all') || buildTargetList.contains(target)
    }
    if(buildTargetList.contains("-$target")) {
        useTarget[target] = false
    }
}

logger.lifecycle(":platforms from: ${os.displayName} on ${arch.displayName}:${buildTargetList}=${useTarget}")
kotlin {

    if (useTarget['jvm']) {
        jvm()
    }
    if (useTarget['js']) {
        js(IR) {
            browser {
                testTask {
                    useKarma {
                        useFirefox()
                    }
                }
            }
            compilations.main {
                kotlinOptions {
                    metaInfo = true
                    sourceMap = true
                    verbose = true
                    moduleKind = "umd"
                }
            }
        }
    }
    if (useTarget['mingw']) {
        mingwX64 {}
    }
    if (useTarget['linux']) {
        if (arch.arm64) {
            linuxArm64()
        } else if (arch.amd64) {
            linuxX64()
        } else {
            logger.error("Cannot use linux on:${arch.name}:${arch.displayName}")
        }
    }
    if (useTarget['macos']) {
        iosArm64()
        macosArm64()
        watchosArm64()
        tvosArm64()
    }
    if (useTarget['wasm-js']) {
        wasmJs {
            browser {
                testTask {
                    useKarma {
                        useFirefox()
                    }
                }
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-annotations-common')
            }
        }
    }
}
