/*
    Copyright 2019-2021 Open JumpCO

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated
    documentation files (the "Software"), to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software,
    and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO
    THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
 */

apply plugin: 'org.jetbrains.kotlin.multiplatform'
def targetList = ['mingw', 'linux', 'macos', 'js', 'jvm', 'wasm-js'/*, 'wasm-wasi'*/]
project.ext.useTarget = [:]
def useTarget = project.ext.useTarget

def profile = ext.find('profile') ?: defaultProfile ?: ''
if (profile.contains('default')) {
    useTarget['jvm'] = true
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        useTarget['mingw'] = true
        logger.lifecycle "Detected ${org.gradle.internal.os.OperatingSystem.current()} using mingw"
    }
    if (org.gradle.internal.os.OperatingSystem.current().isLinux()) {
        useTarget['linux'] = true
        logger.lifecycle "Detected ${org.gradle.internal.os.OperatingSystem.current()} using linux"
    }
    if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        useTarget['macos'] = true
        logger.lifecycle "Detected ${org.gradle.internal.os.OperatingSystem.current()} using macos"
    }
}

targetList.forEach { target ->
    useTarget[target] = profile.contains('all') || (profile.contains(target) && !profile.contains("-$target"))
}

def configureNative(srcSetMain, srcSetTest) {
    srcSetMain.kotlin.srcDirs = ['src/nativeMain/kotlin']
    srcSetTest.kotlin.srcDirs = ['src/nativeTest/kotlin']
}

kotlin {

    if (useTarget['jvm']) {
        jvm() {
            mavenPublication {
                artifactId = "${project.name}-jvm"
            }
        }
    }
    if (useTarget['js']) {
        if (project.ext.has('nodeInstall')) {
            // tasks.findByPath(':compileKotlinsJs')?.dependsOn('kotlinNodeJsSetup')
            tasks.findByPath(':kotlinNodeJsSetup')?.onlyIf { true }
        }
        js(IR) {
            nodejs {
                kotlinNodeJs {
                    nodeVersion = "21.6.2"
                }
            }
            browser {
                testTask {
                    useKarma {
                        useFirefox()
                    }
                }
            }
            compilations.main {
                kotlinOptions {
                    metaInfo = true
                    sourceMap = true
                    verbose = true
                    moduleKind = "umd"
                }
            }
        }
    }
    if (useTarget['mingw']) {
        mingwX64('mingw') {
            mavenPublication {
                artifactId = "${project.name}-mingwX64"
            }
        }
    }
    if (useTarget['linux']) {
        linuxX64('linux') {
            mavenPublication {
                artifactId = "${project.name}-linuxX64"
            }
        }
    }
    if (useTarget['macos']) {
        macosX64('macos') {
            mavenPublication {
                artifactId = "${project.name}-macosX64"
            }
        }
    }
    if (useTarget['wasm-js']) {
        wasmJs {
            browser {
                testTask {
                    useKarma {
                        useFirefox()
                    }
                }
            }
            binaries.executable()
            mavenPublication {
                artifactId = "${project.name}-wasm-js"
            }
        }
    }
    if (useTarget['wasm-wasi']) {
        wasmWasi {
            nodejs()
            mavenPublication {
                artifactId = "${project.name}-wasm-wasi"
            }
        }
        kotlinNodeJs {
            nodeVersion = "21.0.0-v8-canary202309143a48826a08"
            nodeDownloadBaseUrl = "https://nodejs.org/download/v8-canary"
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        if (useTarget['jvm']) {
            jvmMain {
                dependencies {
                    implementation kotlin('stdlib')
                }
            }
            jvmTest {
                dependencies {
                    implementation kotlin('test-junit')
                }
            }
        }
        if (useTarget['js']) {
            jsMain {
                dependencies {
                    implementation kotlin('stdlib-js')
                }
            }
            jsTest {
                dependencies {
                    implementation kotlin('test-js')
                }
                kotlin.srcDirs = ['src/jsTest/kotlin']
            }
        }
        if (useTarget['mingw']) {
            configureNative(mingwMain, mingwTest)
        }
        if (useTarget['linux']) {
            configureNative(linuxMain, linuxTest)
        }
        if (useTarget['macos']) {
            configureNative(macosMain, macosTest)
        }
        if (useTarget['wasm-js']) {
            wasmJsMain.kotlin.srcDirs = ['src/wasmMain/kotlin']
            wasmJsTest.kotlin.srcDirs = ['src/wasmTest/kotlin']
        }
        if (useTarget['wasm-wasi']) {
            wasmWasiMain.kotlin.srcDirs = ['src/wasmMain/kotlin']
            wasmWasiTest.kotlin.srcDirs = ['src/wasmTest/kotlin']
        }
    }
}
