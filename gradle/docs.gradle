/*
 * Copyright (c) 2019. Open JumpCO
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 * You should have received a copy of the GNU General Public License along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.asciidoctor.jvm.convert'

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    configuration {
        includeNonPublic = false

        sourceRoot {
            path = 'src/commonMain/kotlin'
        }
    }
}

asciidoctor {
    inputs.files(fileTree('src/commonTest/kotlin/*'))
    baseDirFollowsSourceDir()
    logDocuments = true
    asciidoctorj {
        fatalWarnings missingIncludes()
    }

    sources {
        include 'index.adoc'
    }

    resources {
        from(sourceDir) {
            include '**/*.png'
            include '**/*.xml'
            include '**/*.js'
            include '**/*.css'
        }
    }

    attributes toc: 'left',
        'source-highlighter': 'prism',
        idprefix: '',
        idseparator: '-',
        docinfo1: ''

    requires ['asciidoctor-prism-extension']

}

task docs(type: Zip, dependsOn: [asciidoctor, dokka]) {
    archiveBaseName = project.name
    archiveClassifier = 'doc'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from(file('build/docs/asciidoc'))

    from(file('build/javadoc')) {
        into('javadoc')
    }
}

publishing {
    publications {
        documentation(MavenPublication) {
            artifact docs
        }
    }
}
