plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.61'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'maven-publish'
    id 'signing'
    id 'org.asciidoctor.jvm.convert' version '2.4.0'
    id 'org.jlleitschuh.gradle.ktlint' version '9.1.1'
    id 'org.jlleitschuh.gradle.ktlint-idea' version '9.1.1'
    id 'com.cosminpolifronie.gradle.plantuml' version '1.6.0'
}

repositories {
    jcenter()
    mavenCentral()
}
group = 'io.jumpco.open'
version = '1.0.1-SNAPSHOT'
description = 'Kotlin Finite-state machine'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

apply from: "$rootProject.projectDir/gradle/platform.gradle"
apply from: "$rootProject.projectDir/gradle/docs.gradle"
apply from: "$rootProject.projectDir/gradle/publish.gradle"

def useTarget = project.ext.useTarget


kotlin {
    sourceSets {
        commonTest {
            dependencies {
                implementation 'io.mockk:mockk-common:1.9.3'
            }
        }
        if (useTarget['jvm']) {
            jvmTest {
                dependencies {
                    implementation 'org.jetbrains.kotlin:kotlin-test:1.3.61'
                    implementation 'junit:junit:4.13'
                    implementation 'io.mockk:mockk:1.9.3'
                }
            }
        }
    }
}


dokka {
    // master configuration in gradle/docsgradle
    samples = [
        'src/commonTest/kotlin/TurnstileTypes.kt',
        'src/commonTest/kotlin/ImmutableLockFSM.kt',
        'src/commonTest/kotlin/LockTypes.kt',
        'src/commonTest/kotlin/PayingTurnstileTypes.kt',
        'src/commonTest/kotlin/SecureTurnstile.kt'
    ]
}

ktlint {
    debug = true
    verbose = true
    reporters {
        reporter "plain"
        reporter "checkstyle"
    }
    // ignoreFailures = true
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}

task copyPng(type:Copy) {
    from (projectDir) {
        include '*.png'
    }
    into "$buildDir/docs/asciidoc"
}

asciidoctor.dependsOn(copyPng)

plantUml {
    render input: 'src/docs/plantuml/*.plantuml', output: 'src/docs/asciidoc', format: 'png', withMetadata: false
}

project.afterEvaluate {
    def envVar = System.getenv('GRAPHVIZ_DOT')
    if (envVar) {
        def dot = file(envVar)
        if (!dot.exists()) {
            throw new RuntimeException("Could not find $dot")
        }
    } else {
        logger.warn("GRAPHVIZ_DOT env var not found")
    }
}
