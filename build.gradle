buildscript {
    repositories {
        mavenLocal()
        maven { url = 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/temporary' }
        maven { url = 'https://maven.pkg.jetbrains.space/kotlin/p/wasm/experimental' }
        maven { url = 'https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven' }
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath 'org.jetbrains.kotlinx:kotlinx-html-jvm:0.10.0'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.20'
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.9.20'
    id 'org.jetbrains.dokka' version '1.9.10'
    id 'maven-publish'
    id 'signing'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.gems' version '3.3.2'
    id 'org.jlleitschuh.gradle.ktlint' version '12.1.0'
    // id 'org.jlleitschuh.gradle.ktlint-idea' version '11.6.1'
    id 'com.cosminpolifronie.gradle.plantuml' version '1.6.0'
    id 'io.jumpco.open.kfsm.viz-plugin' version '1.5.2.4'
    id 'org.jetbrains.qodana' version '2023.3.1'
}

repositories {
    mavenLocal()
    maven { url = 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/temporary' }
    maven { url = 'https://maven.pkg.jetbrains.space/kotlin/p/wasm/experimental' }
    maven { url = 'https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven' }
    mavenCentral()
    ruby {
        gems()
    }
}

group = 'io.jumpco.open'
version = '1.9.0-RC2'
description = 'Kotlin Finite-state machine'

dependencies {
    asciidoctorGems 'rubygems:asciidoctor-tabs:v1.0.0-beta.6'
}
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

apply from: "$rootProject.projectDir/gradle/platform.gradle"
apply from: "$rootProject.projectDir/gradle/docs.gradle"
apply from: "$rootProject.projectDir/gradle/publish.gradle"

def useTarget = project.ext.useTarget


kotlin {
    sourceSets {
        configureEach {
            languageSettings.optIn("kotlin.RequiresOptIn")
        }
        commonMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-common'
                api 'org.jetbrains.kotlin:kotlin-reflect'
                api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.0'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
            }
        }
        if (useTarget['js']) {
            jsMain {
                dependencies {
                    // runtimeOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core-js:1.8.0'
                }
            }
            jsTest {
                dependencies {
                }
            }
        }
        if (useTarget['jvm']) {
            jvmMain {
                dependencies {
                    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
                    // runtimeOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.8.0'

                }
            }
            jvmTest {
                dependencies {
                    implementation 'org.jetbrains.kotlin:kotlin-test'
                    implementation 'io.jumpco.open:kfsm-viz:1.5.2.4'
                }
            }
        }
        if(useTarget['native']) {
            nativeMain {
                dependencies {
                }
            }
        }
        if(useTarget['wasm-js']) {
            wasmJsMain {
                dependencies {
                }
            }
        }
        if(useTarget['wasm-wasi']) {
            wasmWasiMain {
                dependencies {
                }
            }
        }
    }
}

def rootDir = rootProject.projectDir.path
dokkaHtml {
    dokkaSourceSets {
        configureEach {
            samples.from(
                "$rootDir/src/commonTest/kotlin/io/jumpco/open/kfsm/example/TurnstileTypes.kt",
                "$rootDir/src/commonTest/kotlin/io/jumpco/open/kfsm/example/ImmutableLockFSM.kt",
                "$rootDir/src/commonTest/kotlin/io/jumpco/open/kfsm/example/LockTypes.kt",
                "$rootDir/src/commonTest/kotlin/io/jumpco/open/kfsm/example/PayingTurnstileTypes.kt",
                "$rootDir/src/commonTest/kotlin/io/jumpco/open/kfsm/example/SecureTurnstile.kt"
            )
        }
    }
    inputs.dir('src/commonTest/kotlin')
}

ktlint {
    debug = true
    verbose = true
    ignoreFailures = true
    reporters {
        reporter "plain"
        reporter "checkstyle"
    }
    // ignoreFailures = true
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}

task copyPng(type: Copy) {
    from(projectDir) {
        include '*.png'
    }
    into "$buildDir/docs/asciidoc"
}

asciidoctor.dependsOn(copyPng)

plantUml {
    render input: 'src/docs/plantuml/*.plantuml', output: 'src/docs/asciidoc', format: 'png', withMetadata: false
}

project.afterEvaluate {
    def envVar = System.getenv('GRAPHVIZ_DOT')
    if (envVar) {
        def dot = file(envVar)
        if (!dot.exists()) {
            throw new RuntimeException("Could not find $dot")
        }
    } else {
        logger.warn("GRAPHVIZ_DOT env var not found")
    }
}

assemble.dependsOn('generateFsmViz')
docs.dependsOn('generateFsmViz')

kfsmViz {
    fsm('TurnstileFSM') {
        outputFolder = file('generated')
        input = file('src/commonTest/kotlin/io/jumpco/open/kfsm/example/TurnstileTypes.kt')
        isGeneratePlantUml = true
        isGenerateAsciidoc = true
        output = 'turnstile-detail'
    }
    fsm('PayingTurnstileFSM') {
        outputFolder = file('generated')
        input = file('src/commonTest/kotlin/io/jumpco/open/kfsm/example/PayingTurnstileTypes.kt')
        isGeneratePlantUml = true
        isGenerateAsciidoc = true
        output = 'paying-turnstile-detail'
    }

    fsm('SecureTurnstileFSM') {
        outputFolder = file('generated')
        input = file('src/commonTest/kotlin/io/jumpco/open/kfsm/example/SecureTurnstile.kt')
        isGeneratePlantUml = true
        isGenerateAsciidoc = true
        output = 'secure-turnstile-detail'
    }

    fsm('TimerSecureTurnstileFSM') {
        outputFolder = file('generated')
        input = file('src/commonTest/kotlin/io/jumpco/open/kfsm/example/TimeoutSecureTurnstile.kt')
        isGeneratePlantUml = true
        isGenerateAsciidoc = true
        output = 'timeout-turnstile-detail'
    }
    fsm('PacketReaderFSM') {
        outputFolder = file('generated')
        input = file('src/jvmTest/kotlin/io/jumpco/open/kfsm/example/PacketReaderTests.kt')
        isGeneratePlantUml = true
        isGenerateAsciidoc = true
        output = 'packet-reader-detail'
    }
}

dokkaJavadoc.onlyIf { false }
dokkaJekyll.onlyIf { false }
dokkaGfm.onlyIf { false }

