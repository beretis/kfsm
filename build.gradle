plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.4.31'
    id 'org.jetbrains.dokka' version '1.4.10.2'
    id 'maven-publish'
    id 'signing'
    id 'org.asciidoctor.jvm.convert' version '2.4.0'
    id 'org.jlleitschuh.gradle.ktlint' version '9.4.1'
    id 'org.jlleitschuh.gradle.ktlint-idea' version '9.4.1'
    id 'com.cosminpolifronie.gradle.plantuml' version '1.6.0'
    id 'io.jumpco.open.kfsm.viz-plugin' version '1.4.0'
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}
group = 'io.jumpco.open'
version = '1.4.3.1'
description = 'Kotlin Finite-state machine'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

apply from: "$rootProject.projectDir/gradle/platform.gradle"
apply from: "$rootProject.projectDir/gradle/docs.gradle"
apply from: "$rootProject.projectDir/gradle/publish.gradle"

def useTarget = project.ext.useTarget


kotlin {
    sourceSets {
        commonMain {
            dependencies {
                api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
            }
        }
        commonTest {
            dependencies {
                implementation 'io.mockk:mockk-common:1.10.5'
            }
        }
        if (useTarget['js']) {
            jsMain {
                dependencies {
                    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-js:1.4.2'
                }
            }
        }
        if (useTarget['jvm']) {
            jvmMain {
                dependencies {
                    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.4.2'
                }
            }
            jvmTest {
                dependencies {
                    implementation 'org.jetbrains.kotlin:kotlin-test:1.4.21'
                    implementation 'junit:junit:4.13'
                    implementation 'io.mockk:mockk:1.10.5'
                }
            }
        }
    }
}

def rootDir = rootProject.projectDir.path
dokkaHtml {
    dokkaSourceSets {
        configureEach {
            samples.from(
                "$rootDir/src/commonTest/kotlin/TurnstileTypes.kt",
                "$rootDir/src/commonTest/kotlin/ImmutableLockFSM.kt",
                "$rootDir/src/commonTest/kotlin/LockTypes.kt",
                "$rootDir/src/commonTest/kotlin/PayingTurnstileTypes.kt",
                "$rootDir/src/commonTest/kotlin/SecureTurnstile.kt"
            )
        }
    }
    inputs.dir('src/commonTest/kotlin')
}

ktlint {
    debug = true
    verbose = true
    ignoreFailures = true
    reporters {
        reporter "plain"
        reporter "checkstyle"
    }
    // ignoreFailures = true
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}

task copyPng(type: Copy) {
    from(projectDir) {
        include '*.png'
    }
    into "$buildDir/docs/asciidoc"
}

asciidoctor.dependsOn(copyPng)

plantUml {
    render input: 'src/docs/plantuml/*.plantuml', output: 'src/docs/asciidoc', format: 'png', withMetadata: false
}

project.afterEvaluate {
    def envVar = System.getenv('GRAPHVIZ_DOT')
    if (envVar) {
        def dot = file(envVar)
        if (!dot.exists()) {
            throw new RuntimeException("Could not find $dot")
        }
    } else {
        logger.warn("GRAPHVIZ_DOT env var not found")
    }
}

assemble.dependsOn('generateFsmViz')
docs.dependsOn('generateFsmViz')

kfsmViz {
    fsm('TurnstileFSM') {
        outputFolder = file('generated')
        input = file('src/commonTest/kotlin/TurnstileTypes.kt')
        isGeneratePlantUml = true
        isGenerateAsciidoc = true
        output = 'turnstile-detail'
    }
    fsm('PayingTurnstileFSM') {
        outputFolder = file('generated')
        input = file('src/commonTest/kotlin/PayingTurnstileTypes.kt')
        isGeneratePlantUml = true
        isGenerateAsciidoc = true
        output = 'paying-turnstile-detail'
    }

    fsm('SecureTurnstileFSM') {
        outputFolder = file('generated')
        input = file('src/commonTest/kotlin/SecureTurnstile.kt')
        isGeneratePlantUml = true
        isGenerateAsciidoc = true
        output = 'secure-turnstile-detail'
    }

    fsm('TimerSecureTurnstileFSM') {
        outputFolder = file('generated')
        input = file('src/commonTest/kotlin/TimeoutSecureTurnstile.kt')
        isGeneratePlantUml = true
        isGenerateAsciidoc = true
        output = 'timeout-turnstile-detail'
    }
    fsm('PacketReaderFSM') {
        outputFolder = file('generated')
        input = file('src/jvmTest/kotlin/PacketReaderTests.kt')
        isGeneratePlantUml = true
        isGenerateAsciidoc = true
        output = 'packet-reader-detail'
    }
}
